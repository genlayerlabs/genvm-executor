{
  pkgs,
  deps,
  lib,
  stdenvNoCC,
  runnersLib,
  ...
}:
stdenvNoCC.mkDerivation {
  pname = "genvm-ffi";
  version = "3.4.6";

  outputHash = "sha256-lXcDY0HPo1EJNqH1EZuxks1gJkVXdrIWG3i7tTXBtYw=";
  outputHashMode = "recursive";

  srcs = [
    deps."genvm-ffi-src-3.4.6"
    (builtins.path {
      name = "stub_ffi.c";
      path = ./stub_ffi.c;
    })
  ];

  unpackPhase = ''
    for s in $srcs
    do
    echo "src === $s"
    if [[ "$s" == *.c ]]
    then
    cp "$s" ./"$(stripHash "$s")"
    else
    cp -r "$s"/* .
    fi
    done
    chmod -R +w .
  '';

  nativeBuildInputs = [ runnersLib.wasi-sdk.package ];

  configurePhase = ''
    ${runnersLib.wasi-sdk.env-str} CFLAGS="$CFLAGS -Iinclude -Iwasm32-unknown-wasip1 -Iwasm32-unknown-wasip1/include" \
    ./configure \
    "--prefix=$out" \
    --host=wasm32-wasip1
  '';

  buildPhase = ''
    mkdir -p /build/path
    ln -s ${runnersLib.wasi-sdk.package}/bin/ar /build/path/ar
    export PATH="/build/path:$PATH"

    AR_SCRIPT="CREATE libffi.a"

    for i in stub_ffi.c src/closures.c src/prep_cif.c src/tramp.c src/debug.c src/raw_api.c src/types.c
    do
    FNAME="$(basename "$i")"
    clang ${runnersLib.wasi-sdk.env.CFLAGS} \
    -o "$i.o" \
    -fPIC \
    -Iinclude -Iwasm32-unknown-wasip1 -Iwasm32-unknown-wasip1/include \
    -c "$i"
    AR_SCRIPT="$AR_SCRIPT"$'\n'"ADDMOD $i.o"
    done

    AR_SCRIPT="$AR_SCRIPT"$'\n'"SAVE"
    AR_SCRIPT="$AR_SCRIPT"$'\n'"END"

    echo "$AR_SCRIPT" | ar -M
  '';

  installPhase = ''
    mkdir -p "$out/lib"
    mkdir -p "$out/include"

    cp ./wasm32-unknown-wasip1/include/ffitarget.h ./wasm32-unknown-wasip1/include/ffi.h "$out/include"
    cp libffi.a "$out/lib"

    rm -rf "$out/lib/pkgconfig/" || true
    rm -rf "$out/share/man/" || true
  '';

  dontFixup = true;
  dontPatchELF = true;
}
