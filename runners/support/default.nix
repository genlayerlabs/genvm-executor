{
  pkgs,
  stdenvNoCC,
  pkgs-host,
  ...
}@args:
let
  dev-mode = import ./versions/dev-mode.nix;
  runner-scripts = pkgs.lib.cleanSourceWith {
    name = "scripts";
    src = ./scripts;
    filter = pkgs.lib.cleanSourceFilter;
  };
in
rec {
  hashes = import ./versions/current.nix;

  wasi-sdk = import ./tools/wasi-sdk.nix args;

  wasmPatchers = {
    floats-2-soft = import ./tools/genvm-floats-to-soft args;
    add-mod-name = import ./tools/genvm-wasm-add-mod-name args;
  };

  gvm32 = import ./gvm32.nix;
  # dev-mode magic hash: the literal gvm32 string "test" padded with "0"s to 52 chars
  testRunnerHash = "test" + (builtins.concatStringsSep "" (builtins.genList (_: "0") 48));
  hashToIDHash =
    hash:
    if hash == "test" then
      testRunnerHash
    else
      gvm32.encodeHex (
        builtins.convertHash {
          inherit hash;
          toHashFormat = "base16";
        }
      );
  package =
    {
      id,
      hash,
      baseDerivation,
    }:
    {
      inherit id hash;

      uid = "${id}:${hashToIDHash hash}";

      derivation = pkgs-host.stdenvNoCC.mkDerivation (
        {
          name = "genvm_runner_${id}_${hashToIDHash hash}";

          srcs = [
            baseDerivation
            runner-scripts
          ];
          sourceRoot = ".";

          phases = [
            "unpackPhase"
            "buildPhase"
            "installPhase"
          ];

          nativeBuildInputs = with pkgs-host; [ python313 ];

          buildPhase = ''
            out="$(readlink -f ./scripts)/debug-out.zip" ${pkgs-host.python313}/bin/python3 ./scripts/make-zip.py
          '';

          installPhase = ''
            ${pkgs-host.python313}/bin/python3 ./scripts/make-zip.py
          '';

          outputHashMode = "flat";
        }
        // (
          if hash == "test" then
            assert dev-mode;
            { }
          else
            { outputHash = hash; }
        )
      );
    };

  packageWithRunnerJSON =
    {
      id,
      hash,
      baseDerivation,
      expr,
    }:
    package {
      inherit id hash;

      baseDerivation = pkgs-host.symlinkJoin {
        name = "genvm_runner_${id}_${hashToIDHash hash}-merged";
        paths = [
          baseDerivation
          (
            let
              file = pkgs-host.writeText "runner.json" (builtins.toJSON expr);
            in
            pkgs-host.stdenvNoCC.mkDerivation {
              name = "genvm_runner_${id}_${hashToIDHash hash}-runner";

              phases = [ "installPhase" ];
              installPhase = ''
                mkdir -p "$out"
                cp "${file}" "$out/runner.json"
              '';
            }
          )
        ];
      };
    };

  packageGlue =
    {
      id,
      hash,
      expr,
    }:
    package {
      inherit id hash;

      baseDerivation =
        let
          file = pkgs-host.writeText "runner.json" (builtins.toJSON expr);
        in
        pkgs-host.stdenvNoCC.mkDerivation {
          name = "genvm_runner_${id}_${hashToIDHash hash}-runner";

          phases = [ "installPhase" ];
          installPhase = ''
            mkdir -p "$out"
            cp "${file}" "$out/runner.json"
          '';
        };
    };

  buildPy = pkgs.python313;

  toListExcluded = info: item: if info.excludeFromBuild then [ ] else [ item ];
}
