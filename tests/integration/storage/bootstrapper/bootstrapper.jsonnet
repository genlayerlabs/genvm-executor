local simple = import 'templates/simple.jsonnet';
local s = simple.run('${jsonnetDir}/${fileBaseName}.py');
local util = import 'templates/util.jsonnet';
local target_code = importstr './bootstrapper_target.py';

// contract instance slot = sha3_256(ROOT_SLOT_ID(32 zero bytes) + CONTRACT_OFFSET(1 as u32 le))
local contract_instance_slot = 'e1ae23412792df6b6a98fb7c9839701fb8b02b6fc7ed022810f78346494685e7';

{tags: util.features([['storage']], 'stable'),
	entry: util.addPaths([util.chain([
	// deploy bootstrapper
	s {
		message: super.message + {"is_init": true},
	},
	// write i32 value 42 to the contract instance slot at offset 0
	s {
		code: null,
		calldata: |||
			{"": "write", "args": [[(bytes.fromhex('%(slot)s'), 0, (42).to_bytes(4, 'little'))]]}
		||| % {slot: contract_instance_slot},
	},
	// push first half of target contract code
	s {
		code: null,
		vars: {code: target_code},
		calldata: |||
			{"": "push_code", "args": [code[:len(code)//2].encode()]}
		|||,
	},
	// push second half of target contract code
	s {
		code: null,
		vars: {code: target_code},
		calldata: |||
			{"": "push_code", "args": [code[len(code)//2:].encode()]}
		|||,
	},
	// finish bootstrapping (copies code from temp slot to code slot)
	s {
		code: null,
		calldata: |||
			{"": "finish"}
		|||,
	},
	// call get_field on the now-bootstrapped contract
	s {
		code: null,
		calldata: |||
			{"": "get_field"}
		|||,
	},
])])}
